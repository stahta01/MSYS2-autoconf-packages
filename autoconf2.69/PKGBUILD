# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

_realname=autoconf
pkgname=${_realname}2.69
pkgver=2.69
pkgrel=2
pkgdesc="A GNU tool for automatically configuring source code"
arch=('any')
license=('GPL2' 'GPL3' 'custom')
url="https://www.gnu.org/software/autoconf"
depends=('awk' 'm4' 'diffutils' 'bash' 'perl')
makedepends=('gcc' 'make' 'patch')
checkdepends=('gcc-fortran')
source=(https://ftp.gnu.org/pub/gnu/${_realname}/${_realname}-${pkgver}.tar.xz
        0001-fix-texinfo.patch
        0002-msys2.patch
        0003-autotest-remove-cr-from-prog-stdout-stderr.patch
        0004-docs.patch
        0005-package.patch
        0006-autoconf2.5-2.69-perl-5.22-autoscan.patch)
sha256sums=('64ebcec9f8ac5b2487125a86a7760d2591ac9e1d3dbd59489633f9de62a57684'
            'ad42ff57d1ff3dd6ba06fe94f913a369f9210963856e8c955efed9a164609a64'
            '9f31df02eb41df57a05ceea1d1442424d1cca63cd9304c953732fa6c65279a5a'
            '74e2135a3f34f5692a77de85c21cb2f0fd8a1dcf396f7fbba7deb0c59faa54eb'
            'bc973e76f3ea3941818099cb5a22081c8a9703338b3194fce3534a07ac32c078'
            'a9bd30264a812672bf7fa7b67135871ccac4c84b30113b82ea7c2f5987e0d951'
            'a1b6aafd4f42b244f8234b45132b130f26d5518ce202ca903c747a50f0020617')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}

  patch -p1 -i ${srcdir}/0001-fix-texinfo.patch
  patch -p1 -i ${srcdir}/0002-msys2.patch
  patch -p1 -i ${srcdir}/0003-autotest-remove-cr-from-prog-stdout-stderr.patch
  patch -p1 -i ${srcdir}/0004-docs.patch
  patch -p1 -i ${srcdir}/0005-package.patch
  patch -p1 -i ${srcdir}/0006-autoconf2.5-2.69-perl-5.22-autoscan.patch
}

build() {
  cd ${srcdir}/${_realname}-${pkgver}
  ./configure --prefix=/usr --program-suffix=-2.69
  make
}

check() {
  cd ${srcdir}/${_realname}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${_realname}-${pkgver}
  make DESTDIR=${pkgdir} install

  # license exception
  install -Dm644 COPYING.EXCEPTION \
    $pkgdir/usr/share/licenses/autoconf${pkgver}/COPYING.EXCEPTION

  # conflicts with autoconf
  rm -rf ${pkgdir}/usr/share/{emacs,info}
}
